/*
 * Copyright (c) 2025, WSO2 LLC. (http://www.wso2.com) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.apache.tools.ant.taskdefs.condition.Os

description = 'Ballerina - IdeTraceProvider Extension - Ballerina Module'

configurations {
    nativeJar {
        transitive false
    }
    externalJars {
        transitive true
    }
    distribution {
        canBeConsumed true
        canBeResolved false
    }
}

dependencies {
    nativeJar project(':idetraceprovider-extension-native')

    externalJars "io.opentelemetry:opentelemetry-sdk-trace:${openTelemetrySDKVersion}"
    externalJars "io.opentelemetry:opentelemetry-sdk-common:${openTelemetrySDKVersion}"
    externalJars "io.opentelemetry.semconv:opentelemetry-semconv:${openTelemetrySemconvVersion}"
    externalJars "io.opentelemetry:opentelemetry-exporter-otlp:${openTelemetryVersion}"
}

clean {
    delete "${project.projectDir}/target"
    delete "${project.projectDir}/Ballerina.lock"
    delete "${project.projectDir}/ballerina-internal.log"
    delete "${project.projectDir}/lib"
}

jar {
    manifest {
        attributes('Implementation-Title': project.name, 'Implementation-Version': project.version)
    }
}

task copyExternalJarsToLib(type: Copy) {
    into "${project.projectDir}/lib"
    from configurations.externalJars
}

def packageOrg = "ballerinax"
def packageName = "idetraceprovider"
def ballerinaTomlFilePlaceHolder = new File("${project.rootDir}/build-config/resources/Ballerina.toml")
def ballerinaTomlFile = new File("${project.projectDir}/Ballerina.toml")
def artifactBallerinaDocs = file("${project.projectDir}/build/docs_parent/")
def artifactCacheParent = file("${project.buildDir}/cache_parent/")
def artifactLibParent = file("${project.buildDir}/lib_parent/")
def tomlVersion = stripBallerinaExtensionVersion("${project.version}")
def ballerinaCentralAccessToken = System.getenv('BALLERINA_CENTRAL_ACCESS_TOKEN')
def artifactJar = file("$project.projectDir/target/cache/${packageOrg}/${packageName}/${tomlVersion}/java21/")
def platform = "java21"
def skipTests = false

def stripBallerinaExtensionVersion(String extVersion) {
    if (extVersion.matches(project.ext.timestampedVersionRegex)) {
        def splitVersion = extVersion.split('-');
        if (splitVersion.length > 3) {
            def strippedValues = splitVersion[0..-4]
            return strippedValues.join('-')
        } else {
            return extVersion
        }
    } else {
        return extVersion.replace("${project.ext.snapshotVersion}", "")
    }
}

task updateTomlVersions {
    doLast {
        def openTelemetryVersion = project.openTelemetryVersion
        def openTelemetrySDKVersion = project.openTelemetrySDKVersion
        def openTelemetrySemconvVersion = project.openTelemetrySemconvVersion
        def okHttpVersion = project.okHttpVersion
        def okioVersion = project.okioVersion
        def kotlinVersion = project.kotlinVersion

        def newConfig = ballerinaTomlFilePlaceHolder.text.replace("@project.version@", project.version)
        newConfig = newConfig.replace("@toml.version@", tomlVersion)
        newConfig = newConfig.replace("@opentelemetry.version@", openTelemetryVersion)
        newConfig = newConfig.replace("@opentelemetrySDK.version@", openTelemetrySDKVersion)
        newConfig = newConfig.replace("@opentelemetrySemconv.version@", openTelemetrySemconvVersion)
        newConfig = newConfig.replace("@okHttp.version@", okHttpVersion)
        newConfig = newConfig.replace("@okio.version@", okioVersion)
        newConfig = newConfig.replace("@kotlin.version@", kotlinVersion)
        ballerinaTomlFile.text = newConfig
    }
}

task ballerinaBuild {
    dependsOn updateTomlVersions
    dependsOn configurations.nativeJar
    dependsOn configurations.externalJars
    dependsOn copyExternalJarsToLib
    dependsOn compileJava
    dependsOn compileTestJava
    dependsOn jar
    dependsOn checkstyleMain
    dependsOn checkstyleTest
    dependsOn spotbugsMain
    dependsOn spotbugsTest
    dependsOn test
    dependsOn ":idetraceprovider-extension-ballerina:generatePomFileForMavenJavaPublication"

    inputs.dir file(project.projectDir)

    doLast {
        def additionalBuildParams = ""
        if (project.hasProperty("debug")) {
            additionalBuildParams = "--debug ${project.findProperty("debug")}"
        }

        // build bala file
        executeBalCommand ("build", "${project.projectDir}")
        executeBalCommand ("pack", "${project.projectDir}")

        // extract bala file to artifact cache directory
        file("${project.projectDir}/target/bala").eachFileMatch(~/.*.bala/) { balaFile ->
            copy {
                from zipTree(balaFile)
                into file("$artifactCacheParent/bala/${packageOrg}/${packageName}/${tomlVersion}/${platform}")
            }
        }
        copy {
            from file("${project.projectDir}/target/cache")
            exclude '**/*-testable.jar'
            exclude '**/tests_cache/'
            into file("$artifactCacheParent/cache/")
        }

        // Doc creation and packing
        executeBalCommand ("doc", "${project.projectDir}")

        copy {
            from file("$project.projectDir/target/apidocs/${packageName}")
            into file("$project.buildDir/docs_parent/docs/${packageName}")
        }

        boolean publishToCentral = project.hasProperty('publishToCentral') ? project.publishToCentral.toBoolean() : false
        if (publishToCentral) {
            if (project.version.toString().split("-").length > 1) {
                return
            }
            if (ballerinaCentralAccessToken != null) {
                println("Publishing to the ballerina central...")
                def env = "JAVA_OPTS -DBALLERINA_DEV_COMPILE_BALLERINA_ORG=true"
                executeBalCommand ("push", "${project.projectDir}", env)
            } else {
                throw new InvalidUserDataException("Central Access Token is not present")
            }
        }
    }

    outputs.dir artifactCacheParent
    outputs.dir artifactBallerinaDocs
    outputs.dir artifactLibParent
}

task createArtifactZip(type: Zip) {
    destinationDirectory = file("${project.buildDir}/distributions")
    from ballerinaBuild
}

artifacts {
    distribution createArtifactZip
}

task ballerinaPublish {
    dependsOn updateTomlVersions
    dependsOn ballerinaBuild
    dependsOn ":idetraceprovider-extension-ballerina:generatePomFileForMavenJavaPublication"

    doLast {
        if (project.version.toString().split("-").length > 1) {
            return
        }
        if (ballerinaCentralAccessToken != null) {
            println("Publishing to the ballerina central...")
            def env = "JAVA_OPTS -DBALLERINA_DEV_COMPILE_BALLERINA_ORG=true"
            executeBalCommand ("push", "${project.projectDir}", env)
        } else {
            throw new InvalidUserDataException("Central Access Token is not present")
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact source: createArtifactZip, extension: 'zip'
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/${System.getenv("packagePublishRepo")}")
            credentials {
                username = System.getenv("packageUser")
                password = System.getenv("packagePAT")
            }
        }
    }
}

build {
    dependsOn ballerinaBuild
}

publish {
    dependsOn ballerinaPublish
}

task extractBallerinaClassFiles(type: Copy) {
    fileTree(artifactJar).forEach { file ->
        from zipTree(file).matching {
            exclude '**/tests/*'
            include '**/*.class'
        }
        into "${project.rootDir.absolutePath}/build/classes"
    }
}

task publishToLocalCentral {
    dependsOn ballerinaBuild
    doLast {
        if (!skipTests) {
            exec {
                workingDir "${project.projectDir}"
                if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                    commandLine 'cmd', '/c', "bal.bat push --repository=local" +
                            " && exit %%ERRORLEVEL%%"
                } else {
                    commandLine 'sh', '-c', "bal push --repository=local"
                }
            }
        }
    }
}
